plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

group = 'com.seristic'
version = project.property('version')
archivesBaseName = 'LagX'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "glaremasters-repo"
        url = "https://repo.glaremasters.me/repository/towny/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly "com.palmergames.bukkit.towny:towny:0.100.0.0"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    testImplementation "com.palmergames.bukkit.towny:towny:0.100.0.0"
}

test {
    useJUnitPlatform()
}

def targetJavaVersion = 21

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('config.yml') {
        expand props + [configVersion: '0.2.1']
    }
}

tasks.register('relocatedJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    // No relocation needed after full in-source migration
    enabled = false
}

// Build default jar; relocation task disabled